apply from: "$project.rootDir/gradle/string-interpolation.gradle"
apply from: "$project.rootDir/gradle/lombok.gradle"

sourceSets {
    junit {
        java.srcDir 'src/junit/java'
    }
    guice {
        java.srcDir 'src/guice/java'
    }
    logback {
        java.srcDir 'src/logback/java'
    }
    log4j {
        java.srcDir 'src/log4j/java'
    }
    jupiter {
        java.srcDir 'src/jupiter/java'
    }
}

dependencies {
    implementation libs.mockito
    implementation project(':stream-utils')
    implementation project(':reflect-utils')
    implementation project(':generic-utils')

    implementation libs.slf4j
    implementation libs.guava

    annotationProcessor libs.autoService, libs.autoValue
    compileOnly libs.autoService, libs.autoValueAnnotations

    annotationProcessor project(':auto-value-utils')
    compileOnly project.sourceSetDependency(project(':auto-value-utils').sourceSets.annotations)
    compileOnly libs.javaxAnnotationApi

    junitImplementation libs.junit
    junitImplementation project.sourceSetDependency(sourceSets.main)

    logbackImplementation libs.slf4j, libs.logbackClassic
    logbackCompileOnly libs.autoService
    logbackAnnotationProcessor libs.autoService
    logbackImplementation project.sourceSetDependency(sourceSets.main)

    log4jImplementation libs.slf4j, libs.log4jApi, libs.log4jCore, libs.log4jSlf4jBridge
    log4jCompileOnly libs.autoService
    log4jAnnotationProcessor libs.autoService
    log4jImplementation project.sourceSetDependency(sourceSets.main)

    guiceImplementation project.sourceSetDependency(sourceSets.main)
    guiceImplementation project.sourceSetDependency(project(':guice-utils').sourceSets.main)
    guiceImplementation libs.guice
    guiceImplementation libs.junit, libs.mockito
    guiceImplementation project(':stream-utils')
    guiceImplementation project(':generic-utils')
    guiceCompileOnly libs.autoService
    guiceAnnotationProcessor libs.autoService

    jupiterImplementation libs.jupiterApi
    jupiterImplementation libs.autoService
    jupiterAnnotationProcessor libs.autoService
    jupiterImplementation project.sourceSetDependency(sourceSets.main)

    testImplementation libs.jupiterApi, libs.jupiterEngine, libs.junit5Vintage, libs.guice
    testImplementation project.sourceSetDependency(sourceSets.guice)
    testImplementation project.sourceSetDependency(sourceSets.junit)
    testImplementation project.sourceSetDependency(sourceSets.jupiter)
    testImplementation project.sourceSetDependency(sourceSets.logback)
    testImplementation project.sourceSetDependency(sourceSets.log4j)
}

configurations {
    guiceOutput
    guiceImplementation.extendsFrom implementation
    logbackImplementation.extendsFrom implementation
    log4jImplementation.extendsFrom implementation
    jupiterImplementation.extendsFrom implementation
    junitImplementation.extendsFrom implementation
    testImplementation.extendsFrom guiceImplementation, junitImplementation, logbackImplementation, log4jImplementation
}

task guiceOutputJar(type: Jar) {
    from sourceSets.guice.output
}

artifacts {
    guiceOutput guiceOutputJar
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'junit-vintage'
    }
}
